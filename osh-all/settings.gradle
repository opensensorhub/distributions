rootProject.name = 'osh-all'
def oshMainDir = "$rootDir/../.."
gradle.ext.oshCoreDir = "$oshMainDir/osh-addons/core"

// add all modules from the following repos
def repos = ['osh-addons/core', 'osh-addons']
repos.each {
  File repoDir = new File(rootDir, '../../' + it)

  if (!repoDir.exists())
    throw new GradleException('Repository ' + it + ' is missing')
  
  // include all folders with build.gradle as subprojects
  FileTree subprojects = fileTree(repoDir).include('**/build.gradle')
  subprojects.files.each { File f ->
    File projectFolder = f.parentFile
    if (projectFolder != repoDir) {
      String projectName = projectFolder.name
      include projectName
      project(':' + projectName).projectDir = projectFolder
    }
  }
}
